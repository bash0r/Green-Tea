file = [ namespace ] , [ includes ] , [ expression ] ;
space = ? space ? , { ? space ? } ;

identifier = id begin , { id body } ;
id begin = ? /[_a-zA-Z‰ˆ¸ƒ÷‹ﬂ]/ ? ;
id body = id begin | ? /[0-9]/ ? ;

namespace = "namespace" , space , identifier ;
includes = include , { space, include } ;
include = "include " , identifier ;

expression = block | literal | variable exp | condition | function exp | binary operator | select ;
block = empty block | filled block | single block ;
empty block = "{" , [ space ] , "}" ;
filled block = "{" , [ space ] , expression , { space , expression } , [ space ] , "}" ;
single block = "(" , [ space ] , expression , [ space ] , ")" ;

binary operator = expression , [ space ] , operator char , [ space ] , expression ;
operator char = "+" | "-" | "*" | "/" | "^" ;

literal = int literal | float literal | string literal | bool literal | void literal | list literal ;
void literal = "void" ;
int literal = [ "-" ] , digit , { digit } ;
float literal = int literal , "." , digit , { digit } ;
bool literal = "true" | "false" ;
string literal = '"' , { string body } , '"' ;
string body = ? all characters except " and \ ? | "\\" | '\"' ;

variable exp = variable usage | variable init | variable create | variable set | variable export | variable static ;
variable usage = identifier ;
variable init = "var" , space , identifier , [ set ] ;
variable export = "export" , space , identifier , [ set ] ;
variable static = "static" , space , identifier , [ set ] ;
variable set = identifier , set ;
set = [ space ] , "=" , [ space ] , expression ;

condition = if | case
if = "if" , body , [ else ] ;
else = space , "else" , space , expression ;
case = "case" , of , { of } , [ else ] ;
of = space , "of" , body ;
body = space , expression , space , expression ;

function exp = function creation | function application | named application ;
function creation = "func" , [ args ] , space , expression ;
args = [ space ] , "(" , param , { "," , param } , ")" ;
param = [ space ] , identifier , [ space ] ;

function application = ":" , [ space ] , expression , [ space ] , "(" , [ byvals ] , ")" ;
named application = identifier , [ space ] , "(" , [ byvals ] , ")" ;
byvals = byval , { "," , byval } ;
byval = [ space ] , expression , [ space ] ;

list literal = "[" , [ byvals ] , "]" ;

select = from , { from | where } , "select" , space , expression ;
from = "from" , space , identifier , space , "in" , space , expression , space ;
where = "where" , space , expression , space ;
